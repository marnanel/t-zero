Attribute legible;

! XXX Objects in room descriptions should ALWAYS
!      use the "initial" property and never show
!      the name
! XXX Split out grammar extensions to separate file

Extend 'read' first * legible -> Read;

Extend 'leave' first
    * 'no' 'stone' 'unturned' -> LeaveNoStone;

[ ReadSub;
    << Examine noun >>;
    ];

[ LeaveNoStoneSub;

    if (Claw has general) {
        ! We've been here before
        "Once again, you overturn every stone, but to no avail--not even
a nestling centipede is forthcoming.  Every underside becomes a
topside but the overall pattern of stones remains constant.";

    }

    move Claw to location;
    give Claw general;

    "With reckless abandon, you turn over every stone in sight,
avoiding the many small avalanches you launch in the process.
Under the last, overturned stone you find what looks like
the disembodied claw of some prehistoric bird.";

];

Attribute thighpocketwants; ! Your thigh pocket wants this

Object CompassThing "Compass"
with
   name "compass",
   initial "There is a strange geo-temporal compass here.",
   description "The compass contains a needle balanced on a central pivot.
Superimposed on the rim are two concentric sets of markings.
One indicates the 8 cardinal directions.  The other indicates
the 24 hours of the day.  Thus, north corresponds to high 
noon and south to midnight.  A stopwatch-like plunger and a
dual-setting slider cap the whole affair.";

Object PinkSlip "Pink Slip"
has
    legible
with
   name "pink" "slip",
   initial "A pink slip has been hastily abandoned here.",
   description "A faint scribbling, pink on pink, is visible on the slip.  You
remember being handed your walking papers--and, indeed, you have
been walking!",
    before [;
        Consult, Read:
        "In dark pink script, the light pink slip officiously reads:^^
        
        ~Our project is too near completion to allow us to keep in our
        employment a custodian who is prone to sweep subatomic particles
        under the carpet along with the dust.  Also, after repeated
        warnings, you have approached the restricted areas once too
        often.  You may not enter the museum again--your personal
        effects will be delivered to you.~^^
        
        The missive closes with ~Counting down from ten~ and is signed
        with the name Count Zero followed by a flourish and a slashed zero.
        In addition, a single word has been faintly outlined in bold
        capital letters by an impression from light blue carbon paper.
        However, with all that red upon red, the word is not readable in
        the current light.";
    ];

Object JournalPage "Journal Page"
with
    name "journal" "page",
    ! XXX this isn't just the initial
    initial "A page from your journal has been left here where anyone could read it!",
    description "On the journal page you've scribbled the following cryptic instructions:
    ^^
    Shadow the sun . . .
    ^^
    A spire to the sky at noon.^
    Into no man's land at nine.^^
    
    Read in the red light at noon.^
    Transcribe from the violet light at nine.";

Object KhakiFatigues "Khaki Fatigues"
has
    worn proper pluralname container open
with
    name "khaki" "fatigues" "pants",
    description "As befits the acquisitive adventurer,
    your khaki fatigues are
    commodious. Two pockets in particular catch your attention--
    a fob pocket and a thigh pocket.";

Object FobPocket "Fob Pocket." KhakiFatigues
has
    container open
with
    name "fob" "pocket",
    description "Your fob pocket is just the right size for a compass.";

Object ThighPocket "Thigh Pocket." KhakiFatigues
has
    container open
with
    name "thigh" "pocket",
    description
    "Your thigh pocket is not especially spacious and seems to have
    a mind of its own about what goes in and out.",
    react_before [;
        Take: if (noun has thighpocketwants) {
                move noun to self;
                "You put the bad seed in your pocket.  Actually, it's more like
                your pocket swoops out of its lining to grab the bad seed.";
            }
    ];

Object RedSuspenders "Red Suspenders."
has
    worn proper pluralname
with
    name "red" "suspenders" "braces",
    description "Your red suspenders are real snappy.
    Their two elastic bands can be
    crossed over your back to clasp your pants in place.";

Object Poppy "Poppy" FieldOfPoppies
has
    scenery
with
    name "poppy" "flower",
    description "Although brighter than anything else in this muted landscape,
    the poppies still have a washed-out look.  Their crinkled petals
    are slightly brown at the edges and the dry rattleheads of the
    seedpods have scattered without bursting.",
    after [;
        Examine:
            print "^";
            give DryHusk ~concealed;
            print (string) DryHusk.initial;
    ];

Object DryHusk "Dry Husk." FieldOfPoppies
has
    concealed
with
    name "dry" "husk" "seed" "seedpod",
    initial "The dry husk of a hard seedpod, previously unremarked,
    almost escapes your notice.",
    before [;
        Take:
        print "As you take the seed, the seedpod bursts.^^";
        move BadSeed to location;
        remove self;

        << Take BadSeed >>;
        ];

Object BadSeed "Bad Seed."
has
    thighpocketwants
with
    initial "A bad seed lies here, not merely dormant, but dead.",
    description
    "This is a bad seed. Even with the fertilizer from a herd of
    elephants, it's not about to grow.";

Object Stone "Stone" FieldOfStone
has
    scenery
with
    name "stone" "stones",
    description
        "The innumerable stones are evenly spaced across the sloping field.",
    before [;
        Exit:
            ! "Leave stone"
            "Leave well enough alone.";
            ! "Leave no stone unturned" is handled
            ! by LeaveNoStoneSub, q.v.
        ];

Object Claw "Prehistoric Claw"
with
    name "prehistoric" "claw",
    description
        "The webbed husk of leathery, prehistoric claw curls in on itself here.";

